plugins {
    id 'java-library'
    id 'jacoco'
    id "io.codearte.nexus-staging" version "0.21.0"
}

repositories {
    mavenCentral()
}

group = 'io.github.ttony'
version = '1.0.1'

dependencies {
    compile("org.aspectj:aspectjrt:1.9.4")
    compile("org.aspectj:aspectjweaver:1.9.4")

    compile("io.opentracing:opentracing-api:0.32.0")
    compile("io.opentracing:opentracing-util:0.32.0")

    compile("com.google.guava:guava:28.0-jre")
    compile("org.apache.commons:commons-lang3:3.9")



    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'
    testCompile("io.opentracing:opentracing-noop:0.32.0")
    testCompile("io.opentracing:opentracing-mock:0.32.0")


}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

test {
    doFirst {
        def weaver = configurations.compile.find { it.name.contains("aspectjweaver") }
        jvmArgs = jvmArgs << "-javaagent:$weaver"
    }
}

check.dependsOn jacocoTestReport

if (project.hasProperty('release')) {
    // Signing
    apply plugin: 'signing'
    signing {
        sign configurations.archives
    }

    // Deployment
    apply plugin: 'maven'

    task javadocJar(type: Jar) {
        archiveClassifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        archiveClassifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }
    
    uploadArchives {
        repositories {
            mavenDeployer {
                // POM signature
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                // Target repository
                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUser, password: ossrhPassword)
                }
                pom.project {
                    name project.name
                    description project.description
                    packaging 'jar'
                    url 'https://github.com/ttony/opentracing-java-annotation'

                    scm {
                        connection 'scm:git:https://github.com/ttony/opentracing-java-annotation.git'
                        developerConnection 'scm:git:tonytony1@gmail.com:ttony/opentracing-java-annotation.git'
                        url 'https://github.com/ttony/opentracing-java-annotation.git'
                    }

                    licenses {
                        license {
                            name 'The MIT License (MIT)'
                            url 'http://opensource.org/licenses/MIT'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id = 'ttony'
                            name = 'Tony Tony'
                            email = 'tonytony1@gmail.com'
                        }
                    }
                }
            }
        }
    }

    nexusStaging {
        username = ossrhUser
        password = ossrhPassword
    }
}
